eff BuyMilk {
    def buyMilk(): Unit
}

eff BuyBread {
    def buyBread(): Unit
}

eff BuyEggs {
    def buyEggs(): Unit
}

eff BuyScrews {
    def buyScrews(): Unit
}

eff BuyPaint {
    def buyPaint(): Unit
}

eff GetGas {
    def getGas(): Unit
}

eff GroceryList {
    def buyGroceries(): Unit
}

eff HardwareList {
    def buyHardware(): Unit
}

// Handlers
def provideMilk(f: Unit -> a \ ef): a \ (ef - BuyMilk) + IO =
    run { f() } with handler BuyMilk {
        def buyMilk(_, resume) = { println("Buying milk"); resume() }
    }

def provideBread(f: Unit -> a \ ef): a \ (ef - BuyBread) + IO =
    run { f() } with handler BuyBread {
        def buyBread(_, resume) = { println("Buying bread"); resume() }
    }

def provideEggs(f: Unit -> a \ ef): a \ (ef - BuyEggs) + IO =
    run { f() } with handler BuyEggs {
        def buyEggs(_, resume) = { println("Buying eggs"); resume() }
    }

def provideScrews(f: Unit -> a \ ef): a \ (ef - BuyScrews) + IO =
    run { f() } with handler BuyScrews {
        def buyScrews(_, resume) = { println("Buying screws"); resume() }
    }

def providePaint(f: Unit -> a \ ef): a \ (ef - BuyPaint) + IO =
    run { f() } with handler BuyPaint {
        def buyPaint(_, resume) = { println("Buying paint"); resume() }
    }

def provideGas(f: Unit -> a \ ef): a \ (ef - GetGas) + IO =
    run { f() } with handler GetGas {
        def getGas(_, resume) = { println("Getting gas"); resume() }
    }

def provideGroceries(f: Unit -> a \ ef): a \ (ef - GroceryList) + {BuyEggs, BuyMilk, BuyBread} =
    run { f() } with handler GroceryList {
        def buyGroceries(_, resume) = {
            BuyEggs.buyEggs();
            BuyMilk.buyMilk();
            BuyBread.buyBread();
            resume()
        }
    }

def provideHardware(f: Unit -> a \ ef): a \ (ef - HardwareList) + {BuyScrews, BuyPaint} =
    run { f() } with handler HardwareList {
        def buyHardware(_, resume) = {
            BuyScrews.buyScrews();
            BuyPaint.buyPaint();
            resume()
        }
    }

// Split work across two people
def personOne(): String \ GroceryList = {
    GroceryList.buyGroceries();
    "Person One tasks completed"
}

def personTwo(): String \ {HardwareList, GetGas} = {
    HardwareList.buyHardware();
    GetGas.getGas();
    "Person Two tasks completed"
}

// The master errand list:
def thisWeekend(): Unit \ {IO, GroceryList, HardwareList, GetGas} = {
    println(personOne());
    println(personTwo())
}

def main(): Unit \ IO =
    run {
        thisWeekend()
    } with provideGroceries
      with provideHardware
      with provideEggs
      with provideMilk
      with provideBread
      with provideScrews
      with providePaint
      with provideGas
